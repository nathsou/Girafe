If(True, a?, b?) -> a
If(False, a?, b?) -> b

Or(a, b?) -> If(a, True, b)
And(a, b?) -> If(a, b, False)

Not(True?) -> False
Not(False?) -> True
Not(Not(q)?) -> q

Trace(query?) -> @trace(query?)

+(a, b) -> @add(a, b)
-(a, b) -> @sub(a, b)
*(a, b) -> @mult(a, b)
/(a, b) -> @div(a, b)
%(a, b) -> @mod(a, b)
=(a, b) -> @equ(a, b)
>(a, b) -> @gtr(a, b)
>=(a, b) -> @geq(a, b)
<(a, b) -> @lss(a, b)
<=(a, b) -> @leq(a, b)

Range(n) -> Range'(n, Nil)
Range'(0, rng) -> :(0, rng)
Range'(n, rng) -> Range'(-(n, 1), :(n, rng))

Len(lst) -> Len'(lst, 0)
Len'(Nil, n) -> n
Len'(:(h, tl), n) -> Len'(tl, +(n, 1))

Divisible(a, b) -> =(%(a, b), 0)

IsPrime(2) -> True
IsPrime(n) -> If(Or(<(n, 2), Divisible(n, 2)), False, IsPrime'(n, 3))

IsPrime'(n, i) ->
    If(>(*(i, i), n),
        True,
        If(Divisible(n, i),
            False,
            IsPrime'(n, +(i, 2))
        )
    )

Factorize(2) -> :(2, Nil)
Factorize(n) -> 
        If(Divisible(n, 2),
            :(2, Factorize(/(n, 2))),
            Factorize'(n, 3)
        )

Factorize'(n, i) ->
    If(<=(*(i, i), n),
        If(Divisible(n, i),
            :(i, Factorize(/(n, i))),
            Factorize'(n, +(i, 2))
        ),
        :(n, Nil) // n is prime
    )

PrevOdd(n) -> If(Divisible(n, 2), -(n, 1), n)

PrimesLessThan(n) -> If(<(n, 2), Nil, PrimesLessThan'(PrevOdd(n), Nil))
PrimesLessThan'(3, ps) -> :(2, :(3, ps))
PrimesLessThan'(n, ps) -> PrimesLessThan'(-(n, 2), If(IsPrime(n), :(n, ps), ps))