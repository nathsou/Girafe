=(True, True) -> True
=(True, False) -> False
=(False, True) -> False
=(False, False) -> True

Not(True) -> False
Not(False) -> True

And(a, b) -> if(a, b, False)
Or(a, b) -> if(a, True, b)

1 -> S(0)
2 -> S(1)
3 -> S(2)
4 -> S(3)
5 -> S(4)
6 -> S(5)
7 -> S(6)
8 -> S(7)
9 -> S(8)
10 -> S(9)

|(0, x) -> x
|(a, b) -> +(b, *(a, S(9)))

=(0, 0) -> True
=(S(a), S(b)) -> =(a, b)
=(0, S(b)) -> False
=(S(a), 0) -> False
=(S(a), P(b)) -> False

<(0, 0) -> False
<(S(a), 0) -> False
<(0, S(b)) -> True
<(S(a), S(b)) -> <(a, b)
<(P(a), P(b)) -> <(a, b)
<(S(a), P(b)) -> False
<(P(a), S(b)) -> True

>(a, b) -> <(b, a)
>=(a, b) -> Or(>(a, b), =(a, b))
<=(a, b) -> Not(>(a, b))

~(n) -> ~'(n, 0)
~'(n, acc) -> if(>(n, 0), ~'(P(n), P(acc)), acc)

+(a, 0) -> a
+(0, b) -> b
+(a, S(b)) -> S(+(a, b))
+(S(a), b) -> S(+(a, b))
+(S(a), P(b)) -> +(a, b)
+(P(a), S(b)) -> +(a, b)

-(a, b) -> +(a, ~(b))

P(S(n)) -> n
S(P(n)) -> n

*(a, 0) -> 0
*(0, b) -> 0
*(a, S(b)) -> +(a, *(a, b))
*(S(a), b) -> +(b, *(a, b))

**(0, 0) -> Error
**(a, 0) -> 1
**(0, b) -> 0
**(S(0), b) -> 1
**(a, S(0)) -> a
**(a, S(b)) -> *(a, **(a, b))

/(a, 0) -> Error
/(a, S(0)) -> a
/(a, b) -> /'(a, b, 0)
/'(a, b, i) ->
    if(>=(a, b),
        /'(-(a, b), b, S(i)),
        DivRes(i, a)
    )

Quotient(DivRes(q, r)) -> q
Rem(DivRes(q, r)) -> r

Mod(a, b) -> Rem(/(a, b))

IsEven(n) -> =(Mod(n, 2), 0)

IsPrime(S(S(0))) -> True
IsPrime(S(S(S(0)))) -> True
IsPrime(n) -> 
    if(<(n, 2),
        False,
        if(IsEven(n),
            False,
            IsPrime'(n, 3)
        )
    )
IsPrime'(n, i) ->
    if(<=(*(i, i), n),
        if(=(Mod(n, i), 0),
            False,
            IsPrime'(n, +(i, 2))
        ),
        True
    )

Primes(n) -> Primes'(n, 3, :(2, Nil))
Primes'(n, i, primes) -> 
    if(<=(i, n),
        if(IsPrime(i),
            Primes'(n, +(i, 2), :(i, primes)),
            Primes'(n, +(i, 2), primes)
        ),
        primes
    )

Len(Nil) -> 0
Len(:(h, tl)) -> +(Len(tl), 1)

Reverse(lst) -> Reverse'(lst, Nil)
Reverse'(Nil, rev) -> rev
Reverse'(:(h, tl), rev) -> Reverse'(tl, :(h, rev))

N(0) -> 0'
N(1) -> 1'
N(2) -> 2'
N(3) -> 3'
N(4) -> 4'
N(5) -> 5'
N(6) -> 6'
N(7) -> 7'
N(8) -> 8'
N(9) -> 9'

N(n) -> Reverse(N'(n, Nil))
N'(0, acc) -> acc
N'(n, acc) -> N''(/(n, S(9)), acc)
N''(DivRes(q, r), acc) -> :(N(r), N'(q, acc))

N(Nil) -> Nil
N(:(h, tl)) -> :(N(h), N(tl))

Query -> Reverse(N(Primes(**(10, 2))))